buildscript {
    repositories {
        maven {
            url '../../../lib'          // For the DexGuard plugin.
        }
        google()                        // For the Android plugin.
        mavenCentral()                       // For anything else.
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath 'io.realm:realm-gradle-plugin:10.4.0'
        //noinspection GradleDynamicVersion
        classpath 'com.guardsquare:dexguard-gradle-plugin:+'
    }
}

// Note the order of the plugins.
apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'dexguard'

repositories {
    google()  // For the Android support libraries.
    mavenCentral() // For the other libraries.
}

dependencies {
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.0.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
}

android {
    compileSdkVersion 28

    signingConfigs {
        debug {
            storeFile     file('../../debug.keystore')
            storePassword 'android'
            keyAlias      'androiddebugkey'
            keyPassword   'android'
        }
    }

    defaultConfig {
        applicationId 'com.example'
        targetSdkVersion 28
        minSdkVersion 16
        versionCode 1
        versionName "1.0"
        signingConfig signingConfigs.debug
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = ['src']
            aidl.srcDirs         = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs          = ['res']
            assets.srcDirs       = ['assets']
            jniLibs.srcDirs      = ['libs']
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            // Prevent runtime-invisible annotations from being stripped.
            // Please check the following manual page for more information:
            // https://customers.guardsquare.com/en/manual/dexguard/stable/troubleshooting.html#preserveruntimeinvisibleannotations
            minifyEnabled true
            proguardFile "r8-rules.pro"

            shrinkResources false
        }
    }
}

dexguard {
    path = '../../..'
    // We can build the sample with the sample license file
    license = '../../../lib/dexguard-license.txt'
    configurations {
        debug {
            defaultConfiguration 'dexguard-debug.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
        release {
            defaultConfiguration 'dexguard-release.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
    }
}
