buildscript {
    ext.kotlin_version = '1.3.71'
    repositories {
        maven {
            url '../../../lib'          // For the DexGuard plugin.
        }
        google()                        // For the Android plugin.
        mavenCentral()                       // For the Kotlin plugins.
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        //noinspection GradleDynamicVersion
        classpath 'com.guardsquare:dexguard-gradle-plugin:+'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

repositories {
    google()  // For the support libraries.
    mavenCentral() // For the Kotlin runtime library.
}

apply plugin: 'com.android.application'
// Make sure the Kotlin plugins are placed before the DexGuard plugin. Not doing
// so might result in missing classes at run-time.
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'dexguard'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion 28

    signingConfigs {
        debug {
            storeFile     file('../../debug.keystore')
            storePassword 'android'
            keyAlias      'androiddebugkey'
            keyPassword   'android'
        }
    }

    defaultConfig {
        minSdkVersion    15
        targetSdkVersion 29
        applicationId    "com.example"
        versionCode      1
        versionName      "1.0"
        signingConfig signingConfigs.debug
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = ['src']
            aidl.srcDirs         = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs          = ['res']
            assets.srcDirs       = ['assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }
}

dexguard {
    path = '../../..'
    // We can build the sample with the sample license file
    license = '../../../lib/dexguard-license.txt'
    configurations {
        release {
            defaultConfiguration 'dexguard-release-aggressive.pro'
            defaultConfiguration 'dexguard-moshi.pro'
            configuration 'dexguard-project.txt'
        }
        debug {
            defaultConfiguration 'dexguard-debug.pro'
            configuration 'dexguard-project.txt'
        }
    }
}

dependencies {
    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    implementation 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation "com.squareup.retrofit2:converter-moshi:2.3.0"
    implementation "com.squareup.moshi:moshi:1.11.0"
    kapt "com.squareup.moshi:moshi-kotlin-codegen:1.11.0"
}
