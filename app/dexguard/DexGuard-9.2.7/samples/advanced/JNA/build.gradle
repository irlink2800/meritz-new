// The Gradle plugin currently expects the native libraries prepackaged in a
// jar in the libs directory.

buildscript {
    repositories {
        maven {
            url '../../../lib'          // For the DexGuard plugin.
        }
        google()                        // For the Android plugin.
        mavenCentral()                       // For anything else.
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        //noinspection GradleDynamicVersion
        classpath 'com.guardsquare:dexguard-gradle-plugin:+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'dexguard'

repositories {
    flatDir { dirs 'libs' } // For the DexGuard native loader.
    google()                // For the Android support libraries.
    mavenCentral()               // For any other libraries.
}

configurations { natives }

dependencies {
    implementation 'net.java.dev.jna:jna:4.3.0'
    natives files('libs/jna-4.3.0-natives.jar')
    // By including the DexGuard native loader library DexGuard
    // will use the native loader to load encrypted native libraries.
//    implementation(name: 'dexguard-nativeloader', ext: 'aar')
}

android {
    compileSdkVersion 28

    signingConfigs {
        debug {
            storeFile     file('../../debug.keystore')
            storePassword 'android'
            keyAlias      'androiddebugkey'
            keyPassword   'android'
        }
    }

    defaultConfig {
        minSdkVersion    11
        targetSdkVersion 29
        signingConfig signingConfigs.debug
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = ['src']
            aidl.srcDirs         = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs          = ['res']
            assets.srcDirs       = ['assets']
            jniLibs.srcDirs      = ['libs']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }

    packagingOptions {
        exclude  'com/sun/jna/**/*jnidispatch.a'
        exclude  'com/sun/jna/**/*jnidispatch.jnilib'
        exclude  'com/sun/jna/**/*jnidispatch.dll'
    }
}

// This task extracts native libraries from jar dependencies
// added via the 'natives' configuration.
task copyAndroidNatives() {
    configurations.natives.files.each { jar ->
        def outputDir = file("libs")
        copy {
            from zipTree(jar)
            into outputDir
            include "**/*.so"
        }
    }
}

dexguard {
    path = '../../..'
    // We can build the sample with the sample license file
    license = '../../../lib/dexguard-license.txt'
    configurations {
        debug {
            defaultConfiguration 'dexguard-debug.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
        release {
            defaultConfiguration 'dexguard-release-aggressive.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
    }
}
