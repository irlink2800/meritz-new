buildscript {
    repositories {
        maven {
            url '../../../lib'          // For the DexGuard plugin.
        }
        google()                        // For the Android plugin.
        mavenCentral()                       // For anything else.
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        //noinspection GradleDynamicVersion
        classpath 'com.guardsquare:dexguard-gradle-plugin:+'
    }
}

ext {
    androidxVersion   = "1.0.0"
    runnerVersion     = "1.1.0"
    rulesVersion      = "1.1.0"
    espressoVersion   = "3.1.0"
}

repositories {
    google()
    mavenCentral()
}

apply plugin: 'com.android.application'
apply plugin: 'dexguard'

android {
    compileSdkVersion 28

    signingConfigs {
        debug {
            storeFile     file('../../debug.keystore')
            storePassword 'android'
            keyAlias      'androiddebugkey'
            keyPassword   'android'
        }
    }

    defaultConfig {
        minSdkVersion    14
        targetSdkVersion 29
        signingConfig signingConfigs.debug

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = ['src']
            aidl.srcDirs         = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs          = ['res']
            assets.srcDirs       = ['assets']
        }

        androidTest.setRoot('androidTest')
    }

    packagingOptions {
        exclude 'META-INF/maven/com.google.guava/guava/pom.properties'
        exclude 'META-INF/maven/com.google.guava/guava/pom.xml'
    }

    testBuildType "releaseTest"

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
        releaseTest {
            initWith release
            // Workaround to ensure that R8 keeps the @VisibleForTesting annotations
            minifyEnabled true
            proguardFile 'r8-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    // App dependencies
    implementation 'androidx.constraintlayout:constraintlayout:1.1.2'
    implementation 'androidx.annotation:annotation:' + androidxVersion
    implementation 'com.google.guava:guava:18.0'
    implementation 'androidx.legacy:legacy-support-v4:' + androidxVersion
    implementation 'androidx.recyclerview:recyclerview:' + androidxVersion

    // Note that espresso-idling-resource is used in the code under test.
    implementation 'androidx.test.espresso:espresso-idling-resource:' + espressoVersion

    // Testing-only dependencies
    // Force usage of support annotations in the test app, since it is internally used by the runner module.
    androidTestImplementation 'androidx.annotation:annotation:' + androidxVersion
    androidTestImplementation 'androidx.test:runner:' + runnerVersion
    androidTestImplementation 'androidx.test:rules:' + rulesVersion
    androidTestImplementation 'androidx.test.espresso:espresso-core:' + espressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-intents:' + espressoVersion
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:' + espressoVersion
    androidTestImplementation 'androidx.recyclerview:recyclerview:' + androidxVersion
    androidTestImplementation 'androidx.test.espresso:espresso-web:' + espressoVersion
}

dexguard {
    path = '../../..'
    // We can build the sample with the sample license file.
    license = '../../../lib/dexguard-license.txt'
    configurations {
        release {
            defaultConfiguration 'dexguard-release-aggressive.pro'
            configuration 'dexguard-project.txt'
        }
        releaseTest {
            // Include the same configuration as the release configuration
            defaultConfiguration 'dexguard-release-aggressive.pro'
            configuration 'dexguard-project.txt'
            // Extra espresso test configuration
            defaultConfiguration 'dexguard-espresso.pro'
            defaultConfiguration 'dexguard-espresso-no-resource-optimizations.pro'
            defaultConfiguration 'dexguard-espresso-aggressive-optimizations.pro'
            configuration 'dexguard-project-test.txt'
        }
        debug {
            defaultConfiguration 'dexguard-debug.pro'
            configuration 'dexguard-project.txt'
        }
    }
}
