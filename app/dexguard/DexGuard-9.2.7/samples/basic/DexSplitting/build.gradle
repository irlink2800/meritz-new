buildscript {
    repositories {
        maven {
            url '../../../lib'          // For the DexGuard plugin.
        }
        google()                        // For the Android plugin.
        mavenCentral()                       // For anything else.
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        //noinspection GradleDynamicVersion
        classpath 'com.guardsquare:dexguard-gradle-plugin:+'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'dexguard'

repositories {
    google()  // For the Android support libraries.
    mavenCentral() // For any other libraries.
}

android {
    compileSdkVersion 28

    signingConfigs {
        debug {
            storeFile     file('../../debug.keystore')
            storePassword 'android'
            keyAlias      'androiddebugkey'
            keyPassword   'android'
        }
    }

    defaultConfig {
        minSdkVersion    4
        targetSdkVersion 29
        signingConfig signingConfigs.debug
    }

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = ['src']
            aidl.srcDirs         = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs          = ['res']
            assets.srcDirs       = ['assets']
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }
}


dexguard {
    path = '../../..'
    // We can build the sample with the sample license file
    license = '../../../lib/dexguard-license.txt'
    configurations {
        debug {
            // For many applications, you can avoid dex splitting if you just
            // enable shrinking in debug builds. This can even speed up the
            // build, because the initial shrinking step is fast. You do need
            // to make sure to have a working DexGuard configuration for your
            // project.
            //defaultConfiguration 'dexguard-debug-shrink.pro'
            // instead of the default:
            defaultConfiguration 'dexguard-debug.pro'

            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
        release {
            defaultConfiguration 'dexguard-release-aggressive.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
    }
}
